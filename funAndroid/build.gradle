apply plugin: 'com.android.application'

// kotlin-安卓 扩展插件
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.cainiao.funandroid"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        buildConfigField("String", "BUILD_TIME", "\"${new Date().format("yyyyMMdd.HHmmss")}\"")
    }

    signingConfigs {
        debug {
            keyAlias 'fundebugkey'
            keyPassword 'funandroid'
            storeFile file("${rootDir}/debug.keystore")
            storePassword 'funandroid'
        }

        release {
            keyAlias 'funrelease'
            keyPassword 'funandroid'
            storeFile file("${rootDir}/funandroid.keystore")
            storePassword 'funandroid'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            manifestPlaceholders = [label: "Fun 开发"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            manifestPlaceholders = [label: "Fun 正式"]
        }
    }

    /**
     * 解决 Error: Static interface methods are only supported starting with Android N (--min-api 24):
     * void butterknife.Unbinder.lambda$static$0()
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':funcommonlibrary')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation("com.android.support:multidex:1.0.2")

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'


    implementation("com.jakewharton:butterknife:10.0.0")
    annotationProcessor("com.jakewharton:butterknife-compiler:10.0.0")
    /**
     * 使用annotationProcessor 解决
     * Annotation processors must be explicitly declared now.
     * The following dependencies on the compile classpath are found to contain annotation processor.
     * Please add them to the annotationProcessor configuration.
     *   - butterknife-compiler-10.0.0.jar (com.jakewharton:butterknife-compiler:10.0.0)
     *
     * Alternatively, set android.defaultConfig.javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true to continue with previous behavior.
     * Note that this option is deprecated and will be removed in the future.
     */

    implementation("com.airbnb.android:lottie:2.7.0")

    implementation("com.github.Aspsine:SwipeToLoadLayout:1.0.4")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
